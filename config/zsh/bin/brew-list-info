#!/usr/bin/env zsh

emulate -L zsh
autoload -Uz colors && colors

local desc
local -a brew_deps=() dep_list=() packages=()
local -A dependencymap brew_descs

function brew_get_description() {
    if (( $+commands[jq] )) && [[ -z $brew_descs ]]; then
        brew_descs=( ${(z)"$(brew info --json=v1 --installed | jq -j '. [] | .name, " ", @sh "\(.desc)", " "')"} )
    fi
    REPLY=${brew_descs[$1]};
}

brew_deps=( "${(f)"$(brew deps --installed)"}" )

for brew_dep in $brew_deps; do
    dep_list=( "${(s|: |)brew_dep}" )

    package=$dep_list[1]
    deps=( $=dep_list[2] )

    for dep in $deps; do
        if [[ -n $dependencymap[$dep] ]]; then
            dependencymap[$dep]+=" "
        fi

        dependencymap[$dep]+="$package"
    done
done

packages=( "${(f)"$(brew list)"}" )
for package in $packages; do
    brew_get_description $package && desc=$REPLY
    print -l -- "${fg[cyan]}${package}${reset_color} - ${fg[green]}$desc${reset_color}"

    dependencies=( ${=dependencymap[$package]} )

    if [[ -n $dependencies ]]; then
        for dependency in $dependencies; do
            print -l -- "âž¥ ${fg[yellow]}${dependency}${reset_color}"
        done
    fi
done
