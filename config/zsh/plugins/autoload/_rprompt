function _rprompt() {
    local last_status=$?
    local drush_prompt
    local desk_prompt
    local -a rprompt

    _drush_rprompt() {
        local f="${TMPDIR:-/tmp/}/drush-env-${USER}/drush-drupal-site-$$"

        if [[ -f $f ]]; then
            DRUSH_SITE=$(< $f)
        else
            DRUSH_SITE="$DRUPAL_SITE"
        fi

        case "$DRUSH_SITE" in
            *.live|*.prod) local ENV_COLOR="%{$FG[red]%}" ;;
            *.stage|*.test) local ENV_COLOR="%{$FG[yellow]%}" ;;
            *.local|*.dev) local ENV_COLOR="%{$FG[green]%}" ;;
            *) local ENV_COLOR="%{$FG[blue]%}" ;;
        esac

        [[ -n "$DRUSH_SITE" ]] && drush_prompt="${ENV_COLOR}${DRUSH_SITE}%{${FX[reset]}%}"
    }

    _desk_prompt() {
        [[ -n "$DESK_NAME" ]] && desk_prompt="%{${FG[cyan]}%}${DESK_NAME}${FX[reset]}"
    }

    _drush_rprompt
    _desk_prompt

    if [[ ! -z "$TMUX" ]]; then
        tmux_pane=$(tmux display -p "#D" | tr -d %)

        _send_to_tmux() {
            tmux setenv -g "TMUX_$1_${tmux_pane}" "$2"
        }

        _send_to_tmux DRUSH_SITE $DRUSH_SITE
        _send_to_tmux TERMINUS_SITE $TERMINUS_SITE
        _send_to_tmux TERMINUS_ENV $TERMINUS_ENV


        tmux refresh-client -S
    fi

    rprompt=(
        $drush_prompt
        $desk_prompt
    )

    RPROMPT="${(j: - :)rprompt}"
}

_rprompt "$@"
