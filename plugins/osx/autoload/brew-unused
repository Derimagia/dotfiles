#!/usr/bin/env zsh

emulate -L zsh

typeset -a packagedeps deplist packages
typeset -A dependencymap brewinfo

if [[ -n =jq ]]; then
    brewinfo=( ${(z)"$(brew info --json=v1 --installed | jq -j '. [] | .name, " ", @sh "\(.desc)", " "')"} )
fi

packagedeps=( "${(f)"$(brew deps --installed)"}" )

for packagedep in $packagedeps; do
    deplist=( "${(s|: |)packagedep}" )

    package=$deplist[1]
    deps=( $=deplist[2] )

    for dep in $deps; do
        if [[ -n $dependencymap[$dep] ]]; then
            dependencymap[$dep]+=" "
        fi

        dependencymap[$dep]+="$package"
    done
done

packages=( "${(f)"$(brew list)"}" )
for package in $packages; do
    print -l -- "${fg[cyan]}${package}${fg[default]} - $fg[green]${brewinfo[$package]}$fg[default]"

    dependencies=( ${=dependencymap[$package]} )

    if [[ -n $dependencies ]]; then
        for dependency in $dependencies; do
            print -l -- "âž¥ ${fg[yellow]}${dependency}${fg[default]}"
        done
    fi
done
