#!/usr/bin/env zsh

emulate -L zsh
autoload -Uz colors && colors

typeset -a packagedeps deplist packages
typeset -A dependencymap brewinfo

if [[ -n =jq ]]; then
    brewinfo=( ${(z)"$(brew info --json=v1 --installed | jq -j '. [] | .name, " ", @sh "\(.desc)", " "')"} )
fi

packagedeps=( "${(f)"$(brew deps --installed)"}" )

for packagedep in $packagedeps; do
    deplist=( "${(s|: |)packagedep}" )

    package=$deplist[1]
    deps=( $=deplist[2] )

    for dep in $deps; do
        if [[ -n $dependencymap[$dep] ]]; then
            dependencymap[$dep]+=" "
        fi

        dependencymap[$dep]+="$package"
    done
done

packages=( "${(f)"$(brew list)"}" )
for package in $packages; do
    print -l -- "${fg[cyan]}${package}${reset_color} - ${fg[green]}${brewinfo[$package]}${reset_color}"

    dependencies=( ${=dependencymap[$package]} )

    if [[ -n $dependencies ]]; then
        for dependency in $dependencies; do
            print -l -- "âž¥ ${fg[yellow]}${dependency}${reset_color}"
        done
    fi
done
