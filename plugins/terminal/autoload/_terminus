#compdef terminus

function _terminus() {
    local ret=1 cmd="$words[1]" state no_space=0

    function _terminus_subcommands() {
        # If the last word isn't completed (aka "cl" for "cli"), remove it
        if [[ -n "${words[${#words}]}" ]]; then
            words[${#words}]=()
        fi

        local jq_command
        if [[ -z $words[2] ]]; then
            jq_command='.subcommands'
        else
            jq_command='.subcommands | .[] | select(.name == "'$words[2]'") | .subcommands'
        fi
        jq_command+='| .[] | "\(.name):\(.description)"'

        local terminus_options=(${(f)"$(terminus cli cmd-dump --format=json | jq -r $jq_command 2>/dev/null)"})

        if [[ -n $terminus_options ]]; then
            _describe -ot subcommands 'subcommands' terminus_options && ret=0
        fi
    }

    local global_args=(
        '--colorize[Use color in output]'
        '--debug[Get debug output]'
        '--yes[Answer yes to all prompts]'
        '--format[Change the output type to JSON, bash, or silent]'
        ': :_terminus_subcommands'
        '2: :_terminus_subcommands'
    )

    _arguments $global_args

    return ret
}

_terminus "$@"
