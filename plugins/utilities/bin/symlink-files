#!/usr/bin/env zsh

# Symlinks files to a directory

if [[ -z $1 ]] || [[ -z $2 ]]; then
    cat <<EOF
Usage: symlink-files [target files] [destination folder]

Symlinks all target files to a destination
EOF
    return 1
fi

answer_is_yes() {
    [[ "$REPLY" =~ ^[Yy]$ ]] \
            && return 0 \
            || return 1
}

ask_for_confirmation() {
    printf "\e[0;33m  [?] $1 (y/n) \e[0m"
    read -q "REPLY"
    printf "\n"
}

execute() {
    eval "$1" && print_success "$2" || print_error "$2"
}

print_error() {
    # Print output in red
    printf "\e[0;31m  [✖] $1$2\e[0m\n"
}

print_warning() {
    # Print output in yellow
    printf "\e[0;33m  [✔] $1\e[0m\n"
}

print_success() {
    # Print output in green
    printf "\e[0;32m  [✔] $1\e[0m\n"
}

main() {
    filesToSymlink=($1)
    targetFolder="$2"

    if [[ ! -d $targetFolder ]]; then
        print_success "Created directory $targetFolder\n"
        mkdir -p $targetFolder
    fi

    # sourceFile can be a file or a directory
    for sourceFile in ${(z)filesToSymlink}; do
        targetFile="$targetFolder/${sourceFile:t}"

        if [[ -e "$targetFile" ]]; then
            if [[ $targetFile:A != "$sourceFile" ]]; then
                ask_for_confirmation "'$targetFile' already exists, do you want to overwrite it?"
                if answer_is_yes; then
                    # ln can't remove directories so remove it if needed
                    [[ -d  "$targetFile" ]] && rm -rf $targetFile
                    execute "ln -fsn $sourceFile $targetFile" "$targetFile → $sourceFile"
                else
                    print_error "$targetFile → $sourceFile"
                fi
            else
                print_warning "$targetFile → $sourceFile"
            fi
        else
            execute "ln -fsn $sourceFile $targetFile" "$targetFile → $sourceFile"
        fi
    done
}

main $@
