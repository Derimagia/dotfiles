#!/usr/bin/env zsh

# hushlogin
touch ~/.hushlogin

# vscode
local dropbox=~/Dropbox/Apps/VSCode
if [[ -d "$dropbox" ]] {
    ink -c green "[link] Linking VSCode Extension Dir"

    # Symlink vscode to the right directory until https://github.com/Microsoft/vscode/issues/3884 is resolved.
    if [[ ! -L ~/.vscode/extensions ]] {
        ln -sT "$dropbox/extensions" ~/.vscode/extensions
    }

    mkdir -p ~/.vscode
    if [[ ! -L ~/Library/Application\ Support/Code/User/settings.json ]] {
        ln -s "$dropbox/settings.json" ~/Library/Application\ Support/Code/User/settings.json
    }

    if [[ ! -L ~/Library/Application\ Support/Code/User/keybindings.json ]] {
        ln -s "$dropbox/keybindings.json" ~/Library/Application\ Support/Code/User/keybindings.json
    }

    if [[ ! -L ~/Library/Application\ Support/Code/User/snippets ]] {
        ln -sT "$dropbox/snippets" ~/Library/Application\ Support/Code/User/snippets
    }
}

local sublPackages="$XDG_DATA_HOME/Application Support/Sublime Text 3/Packages/User"
if [[ -d ~/Dropbox/Apps/Sublime/User && ! -L "$sublPackages" ]] {
    if [[ ! -d "$sublPackages" ]] {
        ink -c green "[link] Linking Sublime Text Package Dir"
        ln -sT ~/Dropbox/Apps/Sublime/User "$sublPackages"
    } else {
        ink -c red "[link] Can't link - Folder Exists: '$sublPackages'"
    }
}

# virtualbox
if (( $+commands[VBoxManage] )) {
    ink -c green "[virtualbox] setting default folder for virtualbox"
    VBoxManage setproperty machinefolder "$XDG_DATA_HOME/Virtualbox VMs"
}

# dnsmasq
if (( $+commands[dnsmasq] )) && [[ -f $XDG_CONFIG_HOME/dnsmasq/dnsmasq.conf ]] {
    # Need password for dnsmasq
    ink -c green "[dnsmasq] Setting up dnsmasq config"
    sudo -v

    ln -fs $XDG_CONFIG_HOME/dnsmasq/dnsmasq.conf /usr/local/etc/dnsmasq.conf

    # ink -c green "[dnsmasq] Setting up dns resolver"
    # sudo mkdir -p /etc/resolver
    # sudo tee /etc/resolver/test >/dev/null <<-EOF
# nameserver 127.0.0.1
# EOF
}

# grc
if (( $+commands[grc] )) && [[ $OSTYPE =~ darwin ]] && [[ ! -L /usr/local/etc/grc.conf ]] {
    ink -c green "[colors] Symlinking grc config"
    ln -s $XDG_CONFIG_HOME/grc/grc.conf /usr/local/etc/grc.conf
}

# Making some directories
mkdir -p "$XDG_DATA_HOME/node" "$XDG_DATA_HOME/zsh"
